import "Standard" as std;
import "Math";
import "Array";
import "Dictionary";
import "Unity";

const pipeController = Unity.FetchGameObject("PipeController");
const scoreText = Unity.FetchGameObject("Score Text");
const loseText = Unity.FetchGameObject("Lose Text");

loseText.TextMesh.SetText(""); //zero out the text

var spawnTimer = std.Clock();
var pipeArray = Array();
var score = 0;

pipeController.Behaviour.Update = () => {
	//handle game loss
	if (Unity.TimeScale == 0 && loseText.TextMesh.GetText().Length() == 0) {
		loseText.TextMesh.SetText("You Lost!!");
	}
};

pipeController.Behaviour.FixedUpdate = () => {
	//spawn the pipes on a timer
	if (std.Clock() - spawnTimer >= 5) {
		SpawnPipes();
		spawnTimer = std.Clock();
	}

	HandlePipes();
};

const SpawnPipes = () => {
	var pipe = Dictionary();

	var posY = Math.Floor(std.Random() * 6) - 3;

	pipe["gameObject"] = Unity.LoadGameObjectAt("Prefabs/Pipes", 12, posY, 0, 0, 0, 0);
	pipe["spawnTime"] = std.Clock();
	pipe["scored"] = false;

	pipeArray.Push(pipe);
};

const HandlePipes = () => {
	for (var i = 0; i < pipeArray.Length(); i++) {
		//destroy old pipes
		if (std.Clock() - pipeArray[i]["spawnTime"] >= 10) {
			pipeArray[i]["gameObject"].Destroy();
			pipeArray.Delete(i);
			i--; //everything moves down one
		} else {
			pipeArray[i]["gameObject"].Rigidbody2D.PositionX -= 0.05;
			if (pipeArray[i]["gameObject"].Rigidbody2D.PositionX <= 0 && !pipeArray[i]["scored"]) {
				pipeArray[i]["scored"] = true;
				IncrementScore();
			}
		}
	}
};

const IncrementScore = () => {
	score++;
	scoreText.TextMesh.SetText("Score: " + std.ToString(score));
};

